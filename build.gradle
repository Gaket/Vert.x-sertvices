plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '5.1.0'
  id "org.sonarqube" version "2.7.1"
}

group 'com.vyng'
version '0.1'

sourceCompatibility = 1.11

repositories {
  jcenter()
  maven {
    url "https://oss.sonatype.org/content/repositories/iovertx-3844/"
  }
}

ext {
  vertxVersion = '3.8.1'
  junitJupiterEngineVersion = '5.4.0'
}

dependencies {
  implementation "io.vertx:vertx-web:$vertxVersion"
  implementation "io.vertx:vertx-mongo-client:$vertxVersion"
  implementation "io.vertx:vertx-redis-client:$vertxVersion"
  implementation "io.vertx:vertx-auth-common:$vertxVersion"
  implementation "io.vertx:vertx-auth-shiro:$vertxVersion"
  implementation "org.slf4j:slf4j-api:1.7.21"
  implementation "org.slf4j:slf4j-jdk14:1.7.21"
  implementation("com.squareup.okhttp3:okhttp:4.1.0")

  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterEngineVersion"
}

application {
  mainClassName = 'io.vertx.core.Launcher'
}

def mainVerticleName = 'com.vyng.vertex.verticle.HttpServerVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

shadowJar {
  classifier = 'fat'
  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

sonarqube {
  properties {
    properties["sonar.sources"] += "src/main/resources/webroot/"
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'PASSED', 'FAILED', 'SKIPPED'
  }
}

run {
  args = ['run', mainVerticleName, "--launcher-class=$mainClassName", "-conf config/my-application-conf.json", "--redeploy=$watchForChange", "--on-redeploy=$doOnChange"]
}


